TITLE SYSCSS - COMMON SYSTEM SUBROUTINES
SUBTTL 8-9-65

EXTERNAL UXIT,ERROR,USRDDT,USRPOV,PUUOAC,USRREL

;RETURN THE DATE TO THE USER


	INTERNAL DATE,THSDAT

DATE:	MOVE DAT, THSDAT
	JRST STOAC

THSDAT:	0	;CONTAINS THE DATE

;RETURN TIME SYSTEM HAS RUN IN 60THS OF A SECOND

INTERNAL TIMER
EXTERNAL TIME

TIMER:	SKIPA DAT, TIME  ;FALL INTO STOAC

;RETURN DATA SWITCHES

INTERNAL SWITCH

SWITCH:	DATAI DAT

;STORE DAT IN USER AC

INTERNAL STOAC

STOAC:	LDB TAC, PUUOAC
	HRLI TAC, PROG
	MOVEM DAT,@TAC
	JRST UXIT

;RETURN DEVICE CHARACTERISTICS

INTERNAL DVCHR
EXTERNAL JOB,DEVSRC,PJOBN

DVCHR:	MOVE ITEM,JOB
	PUSHJ PDP,DEVSRC	;SEARCH FOR DEVICE
	SKIPA DAT,[0]	;NOT A DEVICE
	SKIPA DAT,DEVMOD(DEVDAT)
	JRST STOAC
	LDB TAC,PJOBN	;GET JOB NUMBER
	CAME TAC,JOB	;DOES USER ALREADY HAVE THIS DEVICE?
	TRNN DAT,ASSCON+ASSPRG	;NO
	TLO DAT,DVAVAL	;NO, BUT HE CAN GET IT
	JRST STOAC

, CALLING SEQUENCE
,	PUSHJ PDP,HASH
,	EXIT;		ALWAYS RETURNS HERE
, COMPUTES XOR HASH TYPE INDEX FROM NAME (SIXBIT CODE) IN AC DAT
, AND LEAVES THE RESULT (00<OR= INDEX <OR=77) AS AN INTEGER IN TAC.
, THE ALGORITHM IS AS FOLLOWS: (V = EXCLUSIVE OR)
, BIT	35:=8V17V26V35
,	34:=7V16V25V34
,	33:=6V15V24V33
,	32:=5V11V23V29
,	31:=4V10V22V28
,	30:=3V9V21V27
, MONITOR INTERFACE
,	SYMBOLS SET/USED:
,		ACCUMULATORS:	DAT U		TAC S/U
,				PDP S/U		TAC1 S/U
;	INTERN HASH


;HASH:	HLR TAC,DAT
;	MOVE TAC1,DAT
;	XORB TAC1,TAC
;	ROT TAC1,-11
;	XOR TAC,TAC1
;	ANDI TAC,UTBNM1
;	POPJ PDP,		;	RETURN

	INTERNAL SETDDT

SETDDT:	HRLI TAC,10000
	MOVEM TAC,USRDDT
	JRST UXIT


	INTERNAL SETPOV

SETPOV:	HRLI TAC,10000
	MOVEM TAC,USRPOV
	JRST UXIT

;WAIT FOR IO TO BECOME INACTIVE ON CHANNEL AC

INTERNAL WAIT
EXTERNAL IOIERR,WAIT1

WAIT:	JUMPE DEVDAT,IOIERR	;CHANNEL ASSIGNED?
	PUSHJ PDP,WAIT1	;WAIT TILL INACTIVE
	JRST UXIT
	END,
